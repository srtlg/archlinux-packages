# Maintainer:  Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: zlbgps<zlbgps@gmail.com>
set -u
pkgname=lfhex
pkgver=0.43
pkgrel=1
pkgdesc='A hex editor with support for large files'
arch=('i686' 'x86_64')
#url='http://stoopidsimple.com/lfhex'
url='https://github.com/srtlg/lfhex'
license=('GPL')
depends=('qt4')
makedepends=('bison' 'flex')
_srcdir="${pkgname}-${pkgver}.tar.gz"
source=("${_srcdir}::${url}/archive/v${pkgver}.tar.gz")
md5sums=('8e8d493d5106a03f20dc19fb4c30eb9a')
sha256sums=('584cf7c6cbeb57acedec1a4f0fe0b607c18748bd574fec106913d686e8f014b8')

prepare() {
  set -u
  cd "${pkgbase}-${pkgver}/src"
  set +u
}

build() {
  set -u
  cd "${pkgbase}-${pkgver}/src"
  qmake-qt4

  # Bison/YACC bug. Different source files expect different file names.
  sed -e '/expr\.tab\.h/ s:mv -f :cp -p :g' -i 'Makefile'

  make
  set +u
}

package() {
  set -u
  cd "${pkgbase}-${pkgver}"

  # Install binary
  install -Dm755 "src/${pkgbase}" "${pkgdir}/usr/bin/${pkgbase}"

  # Install documentation
  install -Dpm644 'CONTRIBUTORS' 'README' -t "${pkgdir}/usr/share/doc/${pkgbase}/"
  set +u
}
set +u
